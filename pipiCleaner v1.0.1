@echo off
:: Türkçe karakter desteði
chcp 1254 > NUL 2>&1

:: Yönetici yetkisi kontrolü ve yükseltme
color 0A
>nul 2>&1 "%SYSTEMROOT%\system32\cacls.exe" "%SYSTEMROOT%\system32\config\system"
if '%errorlevel%' NEQ '0' (
    echo Yönetici yetkisi isteniyor...
    goto UACPrompt
) else ( 
    goto gotAdmin 
)

:UACPrompt
    :: VBScript ile yönetici yetkisi alma
    set "vbsFile=%temp%\getadmin.vbs"
    echo Set UAC = CreateObject^("Shell.Application"^) > "%vbsFile%"
    echo UAC.ShellExecute "%~s0", "", "", "runas", 1 >> "%vbsFile%"
    call "%vbsFile%"
    del "%vbsFile%"
    exit /B

:gotAdmin
    :: Çalýþma dizinini script konumuna ayarla
    pushd "%CD%"
    CD /D "%~dp0"

:: Konsol baþlýðý ve renk ayarý (0A = Siyah arka plan, Açýk yeþil yazý)
title pipiCleaner
color 0A

:: Versiyon deðiþkenini belirle
set "version=v1.0.1"

:: GitHub güncelleme kontrolü
set "repo_owner=scropoolISreal"
set "repo_name=pipiCleaner"

echo.
echo ================================================
echo          Güncellemeler Kontrol Ediliyor
echo ================================================
echo.

for /f "delims=" %%i in ('powershell -Command "Invoke-RestMethod -Uri https://api.github.com/repos/%repo_owner%/%repo_name%/releases/latest | Select-Object -ExpandProperty tag_name"') do set "latest_version=%%i"

if "%latest_version%"=="" (
    echo [X] Güncelleme kontrolü yapýlamadý. Ýnternet baðlantýnýzý kontrol edin.
) else (
    if "%latest_version%" NEQ "%version%" (
        echo [!] Yeni bir sürüm mevcut: %latest_version%
    ) else (
        echo [+] En son sürümü kullanýyorsunuz: %version%
    )
)
echo.

:: ASCII Art Logo ve Program Baþlýðý
echo.
echo            _       _ ________                          
echo     ____  (_)___  (_) ____/ /__  ____ _____  ___  _____
echo    / __ \/ / __ \/ / /   / / _ \/ __ \`/ __ \/ _ \/ ___/
echo   / /_/ / / /_/ / / /___/ /  __/ /_/ / / / /  __/ /    
echo  / .___/_/ .___/_/\____/_/\___/\__,_/_/ /_/\___/_/     
echo /_/     /_/                                            
echo.
echo             pipiCleaner - Sistem Temizlik Aracý %version%
echo ================================================

:: Temizlenecek alanlarýn listesi
echo.
echo Temizlenecek alanlar:
echo - Geçici Dosyalar (Temp Klasörü)
echo - Prefetch Önbelleði
echo - Geri Dönüþüm Kutusu
echo - Son Kullanýlanlar
echo - Windows Güncelleme Önbelleði
echo - Ýnternet Geçici Dosyalarý
echo.

:: Kullanýcý onayý alma
echo Temizliðe baþlamak için 'E' tuþuna, iptal etmek için 'H' tuþuna basýn.
choice /C EH /N /M "Devam etmek istiyor musunuz (E/H)? "

:: Ýptal durumu kontrolü
if %errorlevel%==2 (
    echo.
    echo Ýþlem iptal edildi.
	echo.
    echo Çýkmak için bir tuþa basýn.
    pause >nul 2>&1
    exit
)

echo.
echo Temizlik baþlýyor...
echo.

:: Temp Klasörü Temizleme
echo Geçici dosyalar temizleniyor...
del /f /s /q "%temp%\*.*" >nul 2>&1
for /d %%i in ("%temp%\*") do rd /s /q "%%i" >nul 2>&1
if %errorlevel%==0 (
    echo [+] TEMP klasörü temizlendi
	echo.
    md %temp% >nul 2>&1
) else (
    echo [X] TEMP klasörü temizlenemedi
	echo.
)

:: Prefetch Önbelleði Temizleme
echo Prefetch önbelleði temizleniyor...
del /s /q C:\Windows\Prefetch\*.* >nul 2>&1
if %errorlevel%==0 (
    echo [+] Prefetch önbelleði temizlendi
	echo.
) else (
    echo [X] Prefetch önbelleði temizlenemedi
	echo.
)

:: Geri Dönüþüm Kutusu Temizleme
echo Geri Dönüþüm Kutusu temizleniyor...
rd /s /q %systemdrive%\$Recycle.bin >nul 2>&1
if %errorlevel%==0 (
    echo [+] Geri Dönüþüm Kutusu temizlendi
	echo.
) else (
    echo [X] Geri Dönüþüm Kutusu temizlenemedi
	echo.
)

:: Son Kullanýlanlar Temizleme
echo Son kullanýlanlar temizleniyor...
del /f /s /q %APPDATA%\Microsoft\Windows\Recent\*.* >nul 2>&1
if %errorlevel%==0 (
    echo [+] Son kullanýlanlar temizlendi
	echo.
) else (
    echo [X] Son kullanýlanlar temizlenemedi
	echo.
)

:: Windows Update Önbelleði Temizleme
echo Windows güncelleme önbelleði temizleniyor...
del /s /q C:\Windows\SoftwareDistribution\Download\*.* >nul 2>&1
if %errorlevel%==0 (
    echo [+] Windows güncelleme önbelleði temizlendi
	echo.
) else (
    echo [X] Windows güncelleme önbelleði temizlenemedi
	echo.
)

:: Ýnternet Geçici Dosyalarý Temizleme
echo Ýnternet geçici dosyalarý temizleniyor...
del /s /q "%LOCALAPPDATA%\Microsoft\Windows\INetCache\*.*" >nul 2>&1
if %errorlevel%==0 (
    echo [+] Ýnternet geçici dosyalarý temizlendi
	echo.
) else (
    echo [X] Ýnternet geçici dosyalarý temizlenemedi
	echo.
)

:: Temizlik Sonu
echo.
echo ================================================
echo                Temizlik Tamamlandý!
echo ================================================
echo.
echo Çýkmak için bir tuþa basýn.
pause >nul 2>&1
exit